−−−−−−−−−−−−−−−−−−−−−−−−−−−−

PC-9801シリーズ互換機（N88-日本語BASIC（86））用
　　　　　　　　　　　　　　　　　　※DOS-BASICにも対応

タイミングよく座布団にとびのれ！　ワンキー・ゲーム

　　　　　　　　　　商　　店

−−−−−−−−［嬉しそうな犬］−−−−−−−−−−−−

▼はじめに▲

　テレビ局の前を通り、商店街に入る。パチンコ店の大きな時計が気になると、
角の本屋のところをまがり、ハッとする。
　宇宙の根本原理は「座布団」にあるのではないか、と。


▼正しい遊びかた▲

「座布団」が横から直線的に飛んでくるので、スペース・キーでジャンプして、
タイミングよく上にのってください。「座布団」は主人公に当たると自動的に
消滅する仕組みになっています。
　なお、主人公は『その金さんとやらをだしてもらおうじゃねぇか朝臣人麻呂』
の異名をもつ強者で、どんなに「座布団」のバランスが悪くても決して倒され
ません。


▼プログラムについて▲

　オールBASICです。単純な構造なので、簡単に改造できると思います。260行
にWAITを置いているので、好みの速さに調節してください。


▼その他▲

「座布団」は計10枚飛んできます。全部のれれば御祝儀がもらえますが、1枚
ないし2枚しかのれなかった場合、某『山田』さんが出てきて…。

　リプレイはシフトキーです。

・参考にしたもの
笑点（大喜利）



＜第１表＞　変数表

C%     
Z%     ・・・・・・キャラクター用
YA%

XX　　 ・・・・・・主人公X座標増加量
YY　　 ・・・・・・主人公Y座標増加量
Y　　　・・・・・・主人公Y座標
ZX　　 ・・・・・・座布団X座標
ZY　　 ・・・・・・座布団Y座標
KX　　 ・・・・・・山田さんX座標
ZAV　　・・・・・・座布団獲得数


CHECKER FLAG

Dr.D：これは楽しい。アイデアがいいぞ。ただ、キャラが小さいのが残念だったな。
編：ジャンプの高さも変化できると、なおいいですね。


商店のプログラム・リスト

10 '------ショキセッテイ------
20 SCREEN 3,0:CONSOLE 0,25,0,1:CLS 3:DEFINT A-Z:WIDTH 80,25:COLOR 7,0,0,0,2
30 DIM C%(65),Z%(19),YA%(130):RANDOMIZE (VAL(RIGHT$(TIME$,2)))
40 FOR R=0 TO 2:FOR S=0 TO 7:READ A$:FOR T=0 TO 7
50 C=VAL("&H"+MID$(A$,T+1,1))
60 LINE (T*2+R*16,S*2)-(T*2+1+R*16,S*2+1),C,B:NEXT T,S,R
70 LINE (49,0)-(70,0),3:LINE (48,1)-(71,1),3:LINE (49,2)-(70,2),11
80 GET (0,0)-(15,15),C%:GET (16,0)-(31,15),YA%
90 GET (32,0)-(47,15),YA%(65):GET (48,0)-(71,2),Z%
100 '------ガメンセット------
110 ZAV=1:Z=0:L=0:KX=500:M=0:CLS 3:LINE (100,316)-(540,400),14,BF
120 FOR I=3 TO 7:M=M+I:LINE (100,315+M)-(540,316+M),0,B:NEXT I
130 '------ザブトンセッテイ------
140 XX=-6:Y=300:YY=0:Q=0:ZX=100:J=0:ZY=0
150 R=INT(RND*10+1):IF R<=2 THEN 150
160 '------key ニュウリョク------
170 IF Q=1 THEN:GOTO 190
180 IF INP(&HE9)=191 THEN Q=1:GOTO 220:ELSE 220
190 XX=XX+1:YY=-XX^2+50
200 IF XX=6 THEN Q=0:YY=0:XX=-6
210 GOTO 220
220 '------ヒョウジ------
230 PUT (312,Y-3*Z),C%,XOR
240 Y=300-YY:ZX=ZX+R
250 LINE (ZX-R,313-3*Z)-(ZX-1,315-3*Z),0,BF
260 PUT (312,Y-3*Z),C%,PSET:PUT (ZX,313-3*Z),Z%,PSET:FOR I=0 TO 500:NEXT
270 '------ハンテイ------
280 IF ZX>=500 THEN SH=1:GOTO 310
290 IF ZX>=299 AND ZX<=317 AND Y=275 THEN SH=2:GOTO 310
300 IF ZX>=288 AND ZX<=306 AND Y=300 THEN SH=1
310 IF SH=1 THEN SH=0:PUT (ZX,313-3*Z),Z%,XOR:PUT (312,Y-3*Z),C%,XOR:GOTO 340
320 IF SH=2 THEN SH=0:PUT (ZX,313-3*Z),Z%,PSET:Z=Z+1:ELSE 160
330 PUT (312,Y-3*(Z-1)),C%,XOR
340 ZAV=ZAV+1
350 IF ZAV=11 THEN GOTO 360 ELSE 140
360 '------オワリ------
370 PUT (312,300-3*Z),C%,PSET
380 IF Z=1 OR Z=2 THEN 390 ELSE IF Z=10 THEN 470 ELSE 530
390 FOR K=0 TO 30:KX=KX-(1-2*L)*5
400 LINE (KX-5,300)-(KX+20,315),0,BF
410 PUT (KX,300),YA%(65*L),PSET
420 FOR I=0 TO 700:NEXT I,K
430 IF L=1 THEN 460
440 LINE (288,294)-(341,315),0,BF
450 PUT (312,300),C%,PSET:L=1:FOR I=0 TO 5000:NEXT:GOTO 390
460 LINE (KX-5,300)-(KX+20,315),0,BF:Z=0:GOTO 530
470 LOCATE 36,9:PRINT "┌───┐"
480 LOCATE 36,10:PRINT "│御祝儀│"
490 LOCATE 36,11:PRINT "└───┘"
500 LOCATE 25,12:PRINT Z;"枚達成おめでとうございます。"
510 FOR I=0 TO 100:R=INT(RND*639+1):C=INT(RND*15+1):LINE (320,250)-(R,0),C
520 NEXT:GOTO 540
530 LOCATE 31,10:PRINT "今回は";Z;"枚でした。"
540 IF INP(&HE8)=191 THEN 110:ELSE 540
550 '------DATA------
560 DATA 00FFFF00,0FFFFFF0,00FFFF00,04488CC0
570 DATA 04444CC0,042222C0,04F44FC0,444CCCCC
580 DATA 00FFFF00,0FFFFFF0,00FFFF00,0002A000
590 DATA 00222A00,F2277700,00022000,0022A000
600 DATA 00FFFF00,0FFFFFF0,00FFFF00,000A2033
610 DATA 00A22233,0077722F,00022000,000A2200
620 '[SHOTEN]--------

